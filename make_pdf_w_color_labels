#!perl

# Copyright (C) 2017,2018; Joseph F. Ryan <joseph.ryan@whitney.ufl.edu>
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# for more info see <http://www.gnu.org/licenses/>

use strict;
use warnings;
use Getopt::Long;
use JFR::Fasta;
use Statistics::R;

our $VERSION = 0.05;

MAIN: {
    my $tree = '';
    my $fasta = '';
    my $fan = '';
    my $out = '';
    my $res = Getopt::Long::GetOptions( "tree=s"  => \$tree,
                                        "out=s"   => \$out,
                                        "fan"     => \$fan,
                                        "fasta=s" => \$fasta, );
    usage() unless ($tree && $fasta && $out);
    my $ra_taxa = get_taxa($fasta);
    my $cmds = qq~require(ape)\n~;
    $cmds .= qq~tree<-read.tree("$tree")\n~;
    $cmds .= qq~pdf("$out.pdf")\n~;
    $cmds .= qq~tipcol<-rep('red', length(tree\$tip.label))\n~;
    foreach my $taxon (@{$ra_taxa}) {
        $taxon =~ s/\|/./g;
        $cmds .= qq~tipcol[grep("$taxon",tree\$tip.label)]<-"black"\n~;
    }
    if ($fan) {
        $cmds .= qq~plot(tree, tip.color=tipcol, no.margin = TRUE, label.offset = 0.2, cex = 0.75, type="fan")~;
    } else {
        $cmds .= qq~plot(tree, tip.color=tipcol)~;
    }
    my $r = Statistics::R->new() ;
    $r->run($cmds);
}

sub get_taxa {
    my $file = shift;
    my @taxa = ();
    my $fp = JFR::Fasta->new($file);
    while (my $rec = $fp->get_record()) {
        my $id = JFR::Fasta->get_def_w_o_gt($rec->{'def'});
        push @taxa, $id;
    }
    return \@taxa;
}

sub usage {
    die "usage: $0 --tree=TREEFILE --fasta=PRUNED_FASTA --out=PREFIX [--fan]\n";
}

__END__

=head1 NAME

B<make_pdf_w_color_labels.pl> - create pdf showing which taxa were pruned

=head1 AUTHOR

Joseph F. Ryan <joseph.ryan@whitney.ufl.edu>

=head1 SYNOPSIS

make_pdf_w_color_labels --tree=TREEFILE --fasta=PRUNED_FASTA --out=PREFIX [--fan]

=head1 OPTIONS

=over2

--tree - a treefile in newick format

--fasta - a sequence file in FASTA format. This will be a subset of taxa in the treefile

--out - a PDF file will be made with the prefix supplied here

=back

=head1 BUGS

Please report them to the author

=head1 COPYRIGHT

Copyright (C) 2017,2018; Joseph F. Ryan

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
